# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.collections.bytes-as-array
+home https://github.com/objectionary/eo-collections
+package org.eolang.collections
+rt jvm org.eolang:eo-collections:0.10.2
+version 0.10.2

# Hash code - the pseudo-unique integer representation of an object.
#
# Here "h" must be an object that can be converted to bytes via "as-bytes"
# object.
[h] > hash-code-of
  switch > @
    *
      eq.
        h.as-bytes > h-as-bytes!
        01-
      1231
    *
      h-as-bytes.eq 00-
      1237
    *
      h-as-bytes.size.eq 8
      as-int.
        xor.
          h-as-bytes
          h-as-bytes.right 32
    *
      TRUE
      []
        [acc index bytes zero-as-bytes] > rec-hash-code
          if. > @
            index.eq bytes.length
            acc
            rec-hash-code
              plus.
                times.
                  31
                  acc
                as-int.
                  right.
                    zero-as-bytes.and
                      bytes.at index
                    54
              index.plus 1
              bytes
              zero-as-bytes
        0.as-bytes > zero-as-bytes!
        rec-hash-code > @
          1
          0
          bytes-as-array
            h-as-bytes
          zero-as-bytes
