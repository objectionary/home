# The MIT License (MIT)
#
# Copyright (c) 2016-2022 Yegor Bugayenko
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+home https://github.com/objectionary/eo-collections
+package org.eolang.collections
+rt jvm org.eolang:eo-collections:0.10.2
+version 0.10.2

[arr] > list
  arr > @

  # Is it empty?
  [] > is-empty
    eq. > @
      arr.length
      0

  # Create a new list with this element added to
  # the end of it.
  [x] > with
    list > @
      with.
        arr
        x

  # Create a new list with an element inserted by
  # the provided index.
  [i x] > withi
    concat. > @
      with.
        head.
          ^
          i
        x
      tail.
        ^
        minus.
          arr.length
          i

  # Reduce with index from start "a" using the function "f".
  # Here "f" must be an abstract
  # object with three free attributes. The first
  # one for the accumulator, the second one
  # for the element of the array and the third one for the index.
  [a f] > reducedi
    if. > @
      arr.length.eq 0
      a
      rec-reduced
        * a 0
        f
        arr

    [acc-index func carr] > rec-reduced
      acc-index.at 1 > index
      if. > @
        eq.
          index.plus 1 > next-index
          carr.length
        func > new-acc
          acc-index.at 0
          carr.at index
          index
        rec-reduced
          *
            new-acc
            next-index
          func
          carr

  # Reduce from start "a" using the function "f"
  [a f] > reduced
    ^.reducedi > @
      a
      [a item idx]
        &.f > @
          a
          item

  # Map with index. Here "f" must be an abstract
  # object with two free attributes. The first
  # one for the element of the array, the second one
  # for the index.
  [f] > mappedi
    list > @
      ^.reducedi
        *
        [a item idx]
          with. > @
            a
            &.f item idx

  # Map without index. Here "f" must be an abstract
  # object with one free attribute, for the element
  # of the array.
  [f] > mapped
    ^.mappedi > @
      [x idx]
        &.f x > @

  # For each array element dataize the object
  # Here "f" must be an abstract object with
  # two free attributes: the element of the
  # array and its index.
  [f] > eachi
    seq > @
      ^.reducedi
        TRUE
        [a x i]
          &.f > @
            x
            i
      TRUE

  # For each array element dataize the object
  # Here "f" must be an abstract object with
  # one free attribute, the element of the
  # array.
  [f] > each
    ^.eachi > @
      [x i]
        &.f > @
          x

  # Create a new list without the i-th element
  [i] > withouti
    ^.reducedi > @
      *
      [a item idx]
        if. > @
          idx.eq i
          a
          a.with item

  [x] > without
    list > @
      ^.reduced
        *
        [acc item]
          if. > @
            x.eq item
            acc
            acc.with item

  # Equal to another object?
  [x] > eq
    if. > @
      eq.
        arr.length
        x.length
      ^.reducedi
        TRUE
        [a item idx]
          and. > @
            a
            eq.
              item
              x.at idx
      FALSE

  [passed] > concat
    reduced. > @!
      list
        passed
      ^
      [a x]
        a.with x > @

  # Returns index of the first particular item in list.
  # If the list has no this item, index-of returns -1
  [wanted] > index-of
    ^.reducedi > @
      -1
      [acc item i]
        if. > @
          and.
            eq.
              acc
              -1
            eq.
              item
              wanted
          i
          acc.plus 0

  # Returns index of the last particular item in list.
  # If the list has no this item, last-index-of returns -1
  [wanted] > last-index-of
    ^.reducedi > @
      -1
      [acc item i]
        if. > @
          eq.
            item
            wanted
          i
          acc.plus 0

  # TRUE if the list contain x. Otherwise, "false".
  [x] > contains
    not. > @
      eq.
        -1
        ^.index-of
          x

  # Returns a new list sorted via lt method
  [] > sorted
    reducedi. > res!
      ^
      *
      [a x i]
        a.with (memory i) > @
    memory 0 > i
    memory 0 > j
    while. > sorting-process!
      (i.as-int.plus 1).lt (res.length)
      [iter-i]
        seq > @
          j.write 0
          i.write (i.as-int.plus 1)
          while.
            (j.as-int.plus 2).lt res.length
            [iter-j]
              seq > @
                if.
                  lt.
                    arr.at (res.at (j.as-int.plus 1)).as-int
                    arr.at (res.at j.as-int).as-int
                  swap (res.at j.as-int) (res.at (j.as-int.plus 1))
                  TRUE
                j.write (j.as-int.plus 1)
    seq > @
      sorting-process
      reduced.
        list
          res
        *
        [a x]
          a.with (arr.at x.as-int) > @

    [a b] > swap
      seq > @
        a.as-int > temp!
        a.write b.as-int
        b.write temp

  # Filter list with index with the function "f".
  # Here "f" must be an abstract
  # object with two attributes. The first
  # one for the element, the second one
  # for the index. The result of dataization
  # the "f" should be boolean, that is TRUE or FALSE.
  [f] > filteredi
    if. > @
      eq.
        0
        length.
          arr
      list *
      rec-filtered
        * 0 arr
        f
        list *

    [index-carr func new-list] > rec-filtered
      if. > @
        eq.
          index-carr.at 0 > index
          length.
            index-carr.at 1 > carr
        new-list
        rec-filtered
          *
            index.plus 1
            carr
          func
          if.
            func
              carr.at index > item
              index
            new-list.with item
            new-list

  # Filter list without index with the function "f".
  # Here "f" must be an abstract object
  # with one attribute for the element.
  # The result of dataization the "f"
  # should be boolean, that is TRUE or FALSE.
  [f] > filtered
    ^.filteredi > @
      [item index]
        &.f > @
          item

  [f] > inflated
    rec-inflated > @
      list
        arr
      0
      f

    [a i func] > rec-inflated
      func > result!
        a
        i
      if. > @
        is-empty.
          list
            result
        a
        rec-inflated
          list
            concat.
              list
                a
              result
          plus.
            i
            1
          func

  # Get the first i-th elements from the start of the list
  [i] > head
    i > index!
    0 > zero!
    switch > @
      *
        index.eq zero
        list *
      *
        index.lt zero
        ^.tail
          index.neg
      *
        index.gte
          ^.length
        ^
      *
        TRUE
        list
          ^.reducedi
            *
            [acc item idx]
              if. > @
                idx.gte index
                acc
                acc.with item

  # Get the last i-th elements from
  # the end of the list
  [i] > tail
    minus. > start!
      arr.length
      i
    if. > @
      start.lt 0
      ^
      ^.reducedi
        list *
        [acc item idx]
          if. > @
            gte.
              idx
              start
            acc.with item
            acc

  #
  # TRUE if the current list is lexicographically
  # greater than the passed list. Otherwise, FALSE.
  [other] > gt

  # TRUE if the current list is lexicographically
  # greater than or equal to the passed list. Otherwise, FALSE.
  [other] > gte

  # TRUE if the current list is lexicographically
  # less than the passed list. Otherwise, FALSE.
  [other] > lt

  # TRUE if the current list is lexicographically
  # less than or equal to the passed list. Otherwise, FALSE.
  [other] > lte
