# The MIT License (MIT)
#
#  2016-2022 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.32.0
+version 0.32.0

# Negative infinity
[] > negative-infinity
  -1.0.div 0.0 > @

  # Tests that $ = x
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that $ < x
  [x] > lt
    x > value!
    not. > @
      or.
        value.as-bytes.eq (nan.as-bytes)
        ^.eq value

  # Tests that $ ≤ x
  [x] > lte
    not. > @
      eq.
        x.as-bytes
        nan.as-bytes

  # Tests that $ > x
  [x] > gt
    FALSE > @

  # Tests that $ ≥ x
  [x] > gte
    ^.eq x > @

  # Multiplication of $ and x
  [x...] > times
    # Here we use the rule "NaN != NaN is TRUE" to check if "num" is NaN
    [num] > is-nan
      not. > @
        num.eq num

    [term] > is-nan-or-zero
      or. > @
        is-nan
          term > t!
        t.eq -0.0
        t.eq 0.0
        t.eq 0

    [term] > is-term-gt-zero
      try > @
        []
          term.gt 0 > @
        [e]
          term.gt 0.0 > @
        FALSE

    [acc index terms] > times-rec
      if. > @
        index.eq terms.length
        acc
        if.
          $.&.is-nan-or-zero
            terms.at index > term!
          nan
          $.&.times-rec
            if.
              $.&.is-term-gt-zero term
              acc
              if.
                acc.gt 0.0
                negative-infinity
                positive-infinity
            index.plus 1
            terms
    if. > @
      eq.
        x.length > terms-count!
        0
      negative-infinity
      if.
        terms-count.eq 1
        if.
          is-nan-or-zero
            x.at 0 > first!
          nan
          if.
            is-term-gt-zero first
            negative-infinity
            positive-infinity
        times-rec
          negative-infinity
          0
          x

  # Sum of $ and x
  [x...] > plus
    positive-infinity.as-bytes > pos-inf-as-bytes!

    # Here we use the rule "NaN != NaN is TRUE" to check if "num" is NaN
    [num] > is-nan
      not. > @
        num.eq num

    [term] > is-nan-or-pos-inf
      or. > @
        is-nan
          term > t!
        t.as-bytes.eq pos-inf-as-bytes

    [acc index terms] > plus-rec
      if. > @
        index.eq terms.length
        acc
        if.
          $.&.is-nan-or-pos-inf
            terms.at index
          nan
          $.&.plus-rec
            acc
            index.plus 1
            terms
    if. > @
      eq.
        x.length > terms-count!
        0
      negative-infinity
      if.
        terms-count.eq 1
        if.
          is-nan-or-pos-inf
            x.at 0
          nan
          negative-infinity
        plus-rec
          negative-infinity
          0
          x

  # Difference between $ and x
  [x...] > minus
    negative-infinity.as-bytes > neg-inf-as-bytes!

    # Here we use the rule "NaN != NaN is TRUE" to check if "num" is NaN
    [num] > is-nan
      not. > @
        num.eq num

    [term] > is-nan-or-neg-inf
      or. > @
        is-nan
          term > t!
        t.as-bytes.eq neg-inf-as-bytes

    [acc index terms] > minus-rec
      if. > @
        index.eq terms.length
        acc
        if.
          $.&.is-nan-or-neg-inf
            terms.at index
          nan
          $.&.minus-rec
            acc
            index.plus 1
            terms
    if. > @
      eq.
        x.length > terms-count!
        0
      negative-infinity
      if.
        terms-count.eq 1
        if.
          is-nan-or-neg-inf
            x.at 0
          nan
          negative-infinity
        minus-rec
          negative-infinity
          0
          x

  # Quotient of the division of $ by x
  [x...] > div
    # Here we use the rule "NaN != NaN is TRUE" to check if "num" is NaN
    [num] > is-nan
      not. > @
        num.eq num

    [term] > is-nan-or-infinite
      or. > @
        is-nan
          term > t!
        t.eq positive-infinity
        t.eq negative-infinity

    [term] > is-term-gte-zero
      try > @
        []
          term.gte 0 > @
        [e]
          term.gte 0.0 > @
        FALSE

    [acc index terms] > div-rec
      if. > @
        index.eq terms.length
        acc
        if.
          $.&.is-nan-or-infinite
            terms.at index > term!
          nan
          $.&.div-rec
            if.
              $.&.is-term-gte-zero term
              acc
              if.
                acc.gt 0.0
                negative-infinity
                positive-infinity
            index.plus 1
            terms
    if. > @
      eq.
        x.length > terms-count!
        0
      negative-infinity
      if.
        terms-count.eq 1
        if.
          is-nan-or-infinite
            x.at 0 > first!
          nan
          if.
            is-term-gte-zero first
            negative-infinity
            positive-infinity
        div-rec
          negative-infinity
          0
          x
