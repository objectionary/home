# MIT License
#
# Copyright (c) 2022 Graur Andrew
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.collections.list
+alias org.eolang.txt.text
+alias sprintf org.eolang.txt.sprintf
+home https://github.com/objectionary/eo-hamcrest
+package org.eolang.hamcrest.matchers
+rt jvm org.eolang:eo-hamcrest:0.2.12
+version 0.2.12

# Calculates the logical conjunction of multiple matchers.
# Evaluation is shortcut, so subsequent matchers are not called
# if an earlier matcher returns false.
[matchers...] > all-of-matcher

  memory 0 > mismatch

  [a] > match
    memory 0 > count
    seq > @
      count.write 0
      while.
        and.
          count.lt (matchers.length)
          count.gte 0
        [i]
          if. > @
            (matchers.at i).match a
            count.write (i.plus 1)
            seq
              mismatch.write
                describe-mismatch.
                  matchers.at i
                  a
              count.write -1
      count.gt 0

  [act] > describe-mismatch
    sprintf > @
      "a value %s"
      mismatch

  [] > description-of
    joined. > @
      text " and "
      mapped.
        list
          matchers
        [i]
          i.description-of > @

