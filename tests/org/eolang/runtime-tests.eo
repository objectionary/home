# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+version 0.37.0

# Test.
[] > understands-this-correctly
  # Abstract a.
  [x] > a
    $.x > @
  eq. > @
    a 42
    42

# Test.
[] > takes-parent-object
  # Abstract a.
  [x] > a
    # Take.
    [] > take
      ^.x > @
    take > @
  eq. > @
    a 42
    42

# Test.
[] > makes-object-a-constant
  # Foo.
  [] > foo
    times. > @
      50
      50
  foo.@ > f!
  eq. > @
    f
    f

# Test.
[] > takes-parent-through-attribute
  42 > x
  $ > this
  # Phi1.
  [] > @
    # Phi2.
    [] > @
      # Phi3.
      [] > @
        eq. > @
          this.x
          42

# Test.
[] > throws-when-applies-to-closed-object
  # A.
  [x] > a
    x > @
  a false > closed
  closed true > @

# Test.
[] > makes-deep-object-recursively
  eq. > @
    x 5
    0
  # X.
  [i] > x
    if. > @
      i.lt 0
      0
      x
        i.minus 1

# Test.
[] > calculates-only-once
  eq. > @
    malloc.for
      0
      [m]
        # A.
        [] > a
          seq > @
            *
              ^.m.put (^.m.as-int.plus 1)
              42
        a.neg.neg.neg.neg.eq 42 > @
    1

# Test.
[] > writes-cage-to-malloc
  (cage int.plus).new > f2
  eq. > @
    malloc.of
      8
      [f1]
        malloc.of > @
          8
          [f0]
            seq > @
              *
                f0.put 1
                f1.put 2
                f2.encage (f0.as-int.plus f1)
                f1.put f2
    3

# Test.
[] > recursion-without-arguments
  # Func.
  [n] > func
    if. > @
      n.as-int.gt 0
      seq
        *
          n.put (n.as-int.minus 1)
          ^.func n
      n
  eq. > @
    malloc.for
      4
      func m > [m]
    0

# Test.
[] > compiles-correctly-with-long-duplicate-names
  # Long object 1.
  [] > long-object-name
    # Long object 2.
    [] > long-object-name
      # Long object 3.
      [] > long-object-name
        # Long object 4.
        [] > long-object-name
          # Long object 5.
          [] > long-object-name
  true > @

# Test.
[] > named-inner-abstract-object
  seq > @
    *
      # A.
      [] > a
        true > @

# Test.
[] > app-that-calls-func
  # App.
  [] > app
    # App.f.
    [args] > f
      1 > a
      2 > @
    f > @
      * 1 2 3
  app > output
  eq. > @
    output
    2

# Test.
[] > directly-accesses-objects-from-root
  eq. > @
    Q.org.eolang.malloc.of
      8
      [m]
        seq > @
          *
            m.put 42
            Q.org.eolang.io.stdout
              "Hello, world!"
            m.put
              m.as-int.minus 2
    40

# Test.
[] > directly-accesses-objects-from-standard-root
  eq. > @
    QQ.malloc.of
      8
      [m]
        seq > @
          *
            m.put 42
            QQ.io.stdout
              "Hello, world!"
            m.put
              m.as-int.minus 2
    40

# Test.
[] > standard-root-and-root
  QQ.io.stdout > stand-root
    "one"
  Q.org.eolang.io.stdout > root
    "one"
  eq. > @
    root
    stand-root

# Test.
[] > extract-attribute-from-decoratee
  # Return.
  [foo] > return
  # A.
  [] > a
    return > @
      plus.
        42
        1
  eq. > @
    a.foo
    43

# Test.
[] > constant-defends-against-side-effects
  # Increment.
  [x] > inc
    seq > @
      *
        x.put
          x.as-int.plus 1
        x.as-int
  eq. > @
    malloc.for
      7
      [m]
        int > num
          inc m > n!
        m.put > @
          times.
            num
            num
    64

# Test.
[] > parent-in-vertical-notation
  5 > m
  # Value.
  [] > value
    # Phi.
    [] > @
      m. > @
        ^.
          ^
  eq. > @
    value
    5

# Test.
[] > parent-in-horizontal-notation
  5 > m
  # Value.
  [] > value
    # Phi.
    [] > @
      ^.^.m > @
  eq. > @
    value
    5

# Test.
[] > phi-in-vertical-notation
  # Value.
  [] > value
    # Phi.
    [] > @
      100 > @
  eq. > @
    @.
      value
    100

# Test.
[] > phi-in-horizontal-notation
  # Value.
  [] > value
    # Phi.
    [] > @
      100 > @
  eq. > @
    value.@
    100

# Test.
[] > right-way-to-use-hierarchy
  # Bool mock
  [value] > pybool
    value > @
  # Integer mock.
  [value] > pyint
    # Equal.
    [x] > eq
      pybool (^.value.eq x.value) > @
    # Add.
    [x] > add
      ^.^.pyint (^.value.plus x.value) > @
  ((pyint 1).add (pyint 3)).eq (pyint 4) > @

# Test.
[] > check-triple-quotes
  eq. > @
    """
    Hello

    Hello
    """
    "Hello\n\nHello"

# Test.
[] > correctly-handles-same-name-attrs-simplified
  # Calculate.
  [first second] > calc
    plus. > @
      first
      second
  eq. > @
    calc
      []
        build 1 > @
        # Build first.
        [x] > build
          x > @
      []
        build 2 > @
        # Build second.
        [y] > build
          y > @
    3

# Test.
[] > correctly-handles-same-name-attrs
  # Calculate.
  [f s] > calc
    plus. > @
      f.next
      s.next
  eq. > @
    calc
      []
        # Build first.
        [x] > build
          x.plus 1 > @
          build > next
            plus.
              x
              1
        build 1 > @
      []
        # Build second.
        [y] > build
          y.plus 2 > @
          build > next
            plus.
              y
              2
        build 2 > @
    9
