# The MIT License (MIT)
#
# Copyright (c) 2016-2024 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+tests
+package org.eolang
+version 0.36.0

# Test.
[] > understands-this-correctly
  # Abstract a.
  [x] > a
    $.x > @
  eq. > @
    a 42
    42

# Test.
[] > takes-parent-object
  # Abstract a.
  [x] > a
    # Take.
    [] > take
      ^.x > @
    take > @
  eq. > @
    a 42
    42

# Test.
[] > makes-object-a-constant
  # Foo.
  [] > foo
    times. > @
      50
      50
  foo.@ > f!
  eq. > @
    f
    f

# Test.
[] > takes-parent-through-attribute
  42 > x
  $ > this
  # Phi1.
  [] > @
    # Phi2.
    [] > @
      # Phi3.
      [] > @
        eq. > @
          this.x
          42

# Test.
[] > throws-when-applies-to-closed-object
  # A.
  [x] > a
    x > @
  a FALSE > closed
  closed TRUE > @

# Test.
[] > makes-deep-object-recursively
  eq. > @
    x 5
    0
  # X.
  [i] > x
    if > @
      i.lt 0
      0
      x
        i.minus 1

# Test.
[] > calculates-only-once
  (memory 0).alloc > x
  # A.
  [] > a
    seq > @
      *
        x.write (x.as-int.plus 1)
        42
  seq > @
    *
      x.write 0
      a.neg.neg.neg.neg.eq 42
      and.
        x.eq 1
        x.free

# Test.
[] > writes-cage-to-memory
  (memory 0).alloc > f1
  (cage int.plus).new > f2
  (memory 0).alloc > f0
  seq > @
    *
      f0.write 1
      f1.write 2
      f2.encage ((int f0).plus f1)
      f1.write f2
      and.
        and.
          f1.eq 3
          f1.free
        f0.free

# Test.
[] > recursion-without-arguments
  (memory 0).alloc > n
  # Func.
  [] > func
    if > @
      n.as-int.gt 0
      seq
        *
          n.write (n.as-int.minus 1)
          ^.func
      n
  seq > @
    *
      n.write 4
      and.
        func.eq 0
        n.free

# Test.
[] > compiles-correctly-with-long-duplicate-names
  # Long object 1.
  [] > long-object-name
    # Long object 2.
    [] > long-object-name
      # Long object 3.
      [] > long-object-name
        # Long object 4.
        [] > long-object-name
          # Long object 5.
          [] > long-object-name
  TRUE > @

# Test.
[] > refers-to-inner-vars-correctly
  (memory 0).alloc > go
  seq > @
    *
      go.write TRUE
      while
        go.as-bool
        [i]
          (memory 0).alloc > m
          seq > @
            *
              m.write 42
              go.write FALSE
              m.free
      go.free

# Test.
[] > named-inner-abstract-object
  seq > @
    *
      # A.
      [] > a
        TRUE > @

# Test.
[] > app-that-calls-func
  # App.
  [] > app
    # App.f.
    [args] > f
      1 > a
      2 > @
    f > @
      * 1 2 3
  app > output
  eq. > @
    output
    2

# Test.
[] > directly-accesses-objects-from-root
  (Q.org.eolang.memory 0).alloc > m
  seq > @
    *
      m.write 42
      Q.org.eolang.io.stdout
        "Hello, world!"
      m.write
        m.as-int.minus 2
      and.
        m.eq 40
        m.free

# Test.
[] > directly-accesses-objects-from-standard-root
  (QQ.memory 0).alloc > m
  seq > @
    *
      m.write 42
      QQ.io.stdout
        "Works fine!"
      and.
        eq.
          m.write
            m.as-int.minus 2
          40
        m.free

# Test.
[] > standard-root-and-root
  QQ.io.stdout > stand-root
    "one"
  Q.org.eolang.io.stdout > root
    "one"
  eq. > @
    root
    stand-root

# Test.
[] > extract-attribute-from-decoratee
  # Return.
  [foo] > return
  # A.
  [] > a
    return > @
      plus.
        42
        1
  eq. > @
    a.foo
    43

# Test.
[] > constant-defends-against-side-effects
  (memory 7).alloc > a
  # Increment.
  [x] > inc
    seq > @
      *
        x.write
          x.as-int.plus 1
        x.as-int
  int > num
    inc a > n!
  and. > @
    eq.
      times.
        num
        num
      64
    a.free

# Test.
[] > parent-in-vertical-notation
  (memory 5).alloc > m
  # Value.
  [] > value
    # Phi.
    [] > @
      m. > @
        ^.
          ^
  and. > @
    eq.
      value
      5
    m.free

# Test.
[] > parent-in-horizontal-notation
  (memory 5).alloc > m
  # Value.
  [] > value
    # Phi.
    [] > @
      ^.^.m > @
  and. > @
    eq.
      value
      5
    m.free

# Test.
[] > phi-in-vertical-notation
  # Value.
  [] > value
    # Phi.
    [] > @
      100 > @
  eq. > @
    @.
      value
    100

# Test.
[] > phi-in-horizontal-notation
  # Value.
  [] > value
    # Phi.
    [] > @
      100 > @
  eq. > @
    value.@
    100

# Test.
[] > right-way-to-use-hierarchy
  # Bool mock
  [value] > pybool
    value > @
  # Integer mock.
  [value] > pyint
    # Equal.
    [x] > eq
      pybool (^.value.eq x.value) > @
    # Add.
    [x] > add
      ^.^.pyint (^.value.plus x.value) > @
  ((pyint 1).add (pyint 3)).eq (pyint 4) > @

# Test.
[] > check-triple-quotes
  eq. > @
    """
    Hello

    Hello
    """
    "Hello\n\nHello"

# Test.
[] > correctly-handles-same-name-attrs-simplified
  # Calculate.
  [first second] > calc
    plus. > @
      first
      second
  eq. > @
    calc
      []
        build 1 > @
        # Build first.
        [x] > build
          x > @
      []
        build 2 > @
        # Build second.
        [y] > build
          y > @
    3

# Test.
[] > correctly-handles-same-name-attrs
  # Calculate.
  [f s] > calc
    plus. > @
      f.next
      s.next
  eq. > @
    calc
      []
        # Build first.
        [x] > build
          x.plus 1 > @
          build > next
            plus.
              x
              1
        build 1 > @
      []
        # Build second.
        [y] > build
          y.plus 2 > @
          build > next
            plus.
              y
              2
        build 2 > @
    9
